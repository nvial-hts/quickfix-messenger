plugins {
    id 'java'
    id 'application'
    id 'eclipse'
    id "org.unbroken-dome.xjc" version "2.0.0"
}

version = '3.0-SNAPSHOT'

mainClassName = "com.jramoyo.qfixmessenger.QFixMessenger"

repositories {
    mavenCentral()
}

dependencies {
    // JAXB runtime (Java 8+)
    implementation "javax.xml.bind:jaxb-api:2.3.1"
    implementation "com.sun.xml.bind:jaxb-core:2.3.0.1"
    implementation "com.sun.xml.bind:jaxb-impl:2.3.1"

    // XJC compiler (used by plugin, must go in xjcClasspath)
    xjcClasspath "com.sun.xml.bind:jaxb-xjc:2.3.1"

    implementation "jdom:jdom:1.1"
    implementation "log4j:log4j:1.2.17"
    implementation "org.apache.mina:mina-core:1.1.7"
    implementation "org.slf4j:slf4j-api:1.7.7"
    implementation "org.slf4j:slf4j-log4j12:1.7.7"

    testImplementation "junit:junit:4.11"
    testImplementation "org.hamcrest:hamcrest-core:1.3"

    implementation fileTree(dir: 'lib/compile', include: '*.jar')
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-sources/xjc"
        }
    }
}

xjcGenerate {
    source = fileTree("src/main/xsd") {
        include "*.xsd"
    }
    outputDirectory = file("$buildDir/generated-sources/xjc")
}

tasks.named("compileJava") {
    dependsOn tasks.named("xjcGenerate")
}

clean.doLast {
    delete("log")
    delete("data")
}

cleanEclipse.doLast {
    delete("bin")
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "QuickFIX Messenger",
                "Implementation-Version": version,
                'Main-Class': "com.jramoyo.qfixmessenger.QFixMessenger"
        )
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

applicationDistribution.from("src/main/cfg") {
    into "cfg"
}

applicationDistribution.from("src/main/cfg/log4j.properties") {
    into "lib/log4j"
}

applicationDistribution.from("src/main/dictionary") {
    into "lib/dictionary"
}

applicationDistribution.from("src/main/scripts") {
    into "."
}

startScripts {
    classpath += files("./dictionary")
    classpath += files("./log4j")
}

println "XSD files: " + fileTree("src/main/xsd").files

tasks.register("runAcceptor", JavaExec) {
    group = tasks.run.group
    description = "Run avec arguments Acceptor"

    mainClass = tasks.run.mainClass
    classpath = sourceSets.main.runtimeClasspath +
            files("cfg/acceptor")
    args "cfg/acceptor/messenger.cfg", "cfg/acceptor/quickfix.cfg"
}

tasks.register("runInitiator", JavaExec) {
    group = tasks.run.group
    description = "Run avec arguments Initiator"

    mainClass = tasks.run.mainClass
    classpath = sourceSets.main.runtimeClasspath +
            files("cfg/initiator")   // <-- ajoutÃ© au classpath
    args "cfg/initiator/messenger.cfg", "cfg/initiator/quickfix.cfg"
}
